/* This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/. */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion Config.compileSdkVersion

    defaultConfig {
        applicationId "org.mozilla.samples.browser"
        minSdkVersion Config.minSdkVersion
        targetSdkVersion Config.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "abi"

    productFlavors {
        // Processor architecture
        arm {
            dimension "abi"
        }
        x86 {
            dimension "abi"
        }
    }

    variantFilter { variant ->
        if (variant.buildType.name == "release") {
            // This is a sample app that we are not releasing. Save some time and do not build
            // release versions.
            setIgnore(true)
        }
    }
}


dependencies {
    implementation project(':concept-engine')

    implementation project(':browser-engine-system')
    implementation project(':browser-state')

    implementation project(':ui-autocomplete')
    implementation project(':support-utils')

    armImplementation Gecko.geckoview_nightly_arm
    x86Implementation Gecko.geckoview_nightly_x86

    implementation Dependencies.kotlin_stdlib
    implementation Dependencies.kotlin_coroutines

    implementation Dependencies.support_appcompat
    implementation Dependencies.support_constraintlayout
}
